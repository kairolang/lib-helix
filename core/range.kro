///--- The Kairo Project ------------------------------------------------------------------------///
///                                                                                              ///
///   Part of the Kairo Project, under the Attribution 4.0 International license (CC BY 4.0).    ///
///   You are allowed to use, modify, redistribute, and create derivative works, even for        ///
///   commercial purposes, provided that you give appropriate credit, and indicate if changes    ///
///   were made.                                                                                 ///
///                                                                                              ///
///   For more information on the license terms and requirements, please visit:                  ///
///     https://creativecommons.org/licenses/by/4.0/                                             ///
///                                                                                              ///
///   SPDX-License-Identifier: CC-BY-4.0                                                         ///
///   Copyright (c) 2024 The Kairo Project (CC BY 4.0)                                           ///
///                                                                                              ///
///------------------------------------------------------------------------------------ Kairo ---///
///
/// ### Overview
///
/// This module is part of the **Kairo Core** library, meaning its functionality is natively
/// available to all Kairo programs without requiring explicit imports. It provides the
/// foundational constructs for defining and working with ranges, integral to many idiomatic
/// Kairo operations.
///
/// A notable feature of this module is its integration with the range operators (`..` and `..=`),
/// which implicitly leverage the `range` functions defined here. These operators offer a concise
/// and expressive way to generate ranges, making them suitable for iteration, slicing, and other
/// operations that require sequential values.
///
/// ### Examples
///
/// ```Kairo
/// for x in range(10) {
///     print(x); // Prints 0 through 9
/// }
///
/// for x in range(2, 10, 2) {
///     print(x); // Prints 2, 4, 6, 8
/// }
///
/// var inclusive_range = [x for x in ..=10];
/// print(inclusive_range); // Prints [0, 1, 2, ..., 10]
///
/// var exclusive_range = [x for x in 5..15];
/// print(exclusive_range); // Prints [5, 6, 7, ..., 14]
/// ```
///
/// ### Implicit Integration with Range Operators
///
/// The range operators `..` (exclusive) and `..=` (inclusive) automatically invoke the
/// corresponding `range` functions defined in this module:
///
/// - `..` generates a range from the default value `T()` to an exclusive upper bound.
/// - `..=` generates a range from the default value `T()` to an inclusive upper bound.
///
/// For example:
/// ```Kairo
/// for x in 0..5 { // Equivalent to range(5)
///     print(x);
/// }
///
/// for x in 1..=5 { // Equivalent to range(1, 6)
///     print(x);
/// }
/// ```
///
/// ### Interface: RangeCompliant
///
/// The `RangeCompliant` interface defines the following requirements:
///
/// ```Kairo
/// interface RangeCompliant {
///     fn op ++ (self)              -> self;  // Increment operation.
///     fn op <  (self, other: self) -> bool;  // Less-than comparison.
/// }
/// ```
///
/// Implementing this interface ensures that the type can be used with `range` functions and
/// range operators, facilitating seamless integration across custom and core types.
///
/// ### Function: range(last)
///
/// Generates values from `T()` (the default constructor) up to, but not including, `last`.
///
/// ```Kairo
/// fn <T> range(last: T) -> yield T
///   requires  impl RangeCompliant;
/// ```
///
/// **Parameters:**
/// - `last`: The exclusive upper bound of the range.
///
/// **Requirements:**
/// - `T` must satisfy the `RangeCompliant` interface.
///
/// **Behavior:**
/// - Starts from `T()` and increments the value until `first < last` is false.
/// - Uses the `++` operator for incrementing.
///
/// ### Function: range(first, last, step)
///
/// Generates values from `first` to, but not including, `last`, with a customizable step value.
///
/// ```Kairo
/// fn <T> range(first: T, last: T, step: T = 1) -> yield T
///   requires impl RangeCompliant;
/// ```
///
/// **Parameters:**
/// - `first`: The inclusive lower bound of the range.
/// - `last`: The exclusive upper bound of the range.
/// - `step`: The increment applied to `first` in each iteration. Defaults to 1.
///
/// **Requirements:**
/// - `T` must satisfy the `RangeCompliant` interface.
///
/// **Behavior:**
/// - Starts from `first` and increments it by `step` until `first < last` is false.
/// - If `step` is specified, the function iterates over the range to increment `first` accordingly.
///
/// ### Design Philosophy
///
/// By integrating with the core Kairo operators and adhering to Kairo's trait-based type system,
/// this module provides a robust and extensible solution for generating and working with ranges.
/// It exemplifies Kairo's commitment to type safety, expressiveness, and seamless interoperability.
///
/// ### Contributing
///
/// Contributions to enhance the `range` API or extlast its functionality are welcome.
/// Please adhere to the Kairo Project's contribution guidelines and ensure that all additions
/// maintain consistency with the project's coding standards.
///
/// For questions or discussions about this module, join the community forums at:
/// https://kairo-lang.com/community
#[trivially_import(true)] // prevents wrapping IR in a `unwind` module, keeping the handler globally accessible

module std {
    class <T impl std::Interface::RangeCompliant> Range {
        var first: T;
        var last:  T;
        var step:  isize;

        fn Range(self, first: T, last: T, step: isize = 1) {
            self.first = first;
            self.last  = last;
            self.step  = step;
        }

        fn Range(self, last: T) {
            self.first = T();
            self.last  = last;
            self.step  = 1;
        }

        fn op in (self)[iter] -> yield T {
            var current = self.first;

            while current < self.last {
                yield current;
                
                if (self.step < 0) {
                    for (var _: isize = 0; _ > self.step; ++_) {
                        ++current;
                    }
                } else {
                    for (var _: isize = 0; _ < self.step; ++_) {
                        ++current;
                    }
                }
            }
        }

        fn op in (self, value: T)[contains] -> bool {
            return value >= self.first && value < self.last;
        }
    }
}

fn <T impl std::Interface::RangeCompliant> range(last: T) -> std::Range::<T> {
    return std::Range::<T>(last);
}

fn <T impl std::Interface::RangeCompliant> range(first: T, last: T, step: isize = 1) -> std::Range::<T> {
    return std::Range::<T>(first, last, step);
}

// todo: make strings codegen to kairo slices or wchar_t* in C++